plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.3.2'
}

group 'com.rundeck'

ext.rundeckPluginVersion = '1.2'
ext.pluginClassNames='MesospherePutAppStepPlugin,MesosphereGetTasksStepPlugin,MesosphereDeleteTasksStepPlugin,MesosphereDeleteAppStepPlugin,MesosphereCleanupStepPlugin,ResourceModelFactory'
ext.pluginName = 'Mesos Step Plugin'
ext.pluginDescription = 'Workflow steps to create and use apps and tasks using Mesos API'

//sourceCompatibility = 1.8

scmVersion {
    tag {
        prefix = ''
        versionSeparator = ''
        def origDeserialize=deserialize
        //apend .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}
project.version = scmVersion.version

apply plugin: 'groovy'
apply plugin: 'java'

sourceSets {
    main {
        groovy {
            srcDirs = ['contents']
        }

        java {
            srcDirs = ['contents']
        }
    }
}

repositories {
    mavenCentral()
}

configurations {
    pluginLibs

    compile {
        extendsFrom pluginLibs
    }
}


dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    testCompile group: 'junit', name: 'junit', version: '4.12'

    pluginLibs group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    pluginLibs group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.2'

    pluginLibs group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.1'
    pluginLibs group: 'org.apache.httpcomponents', name:  'httpcore', version:'4.4.5'
    pluginLibs group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.1'
    pluginLibs group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    pluginLibs group: 'com.google.guava', name: 'guava', version: '18.0'
    pluginLibs group: 'com.codahale.metrics', name: 'metrics-core', version: '3.0.1'

    compile group: 'org.rundeck', name: 'rundeck-core', version: '2.10.1'

    pluginLibs('org.codehaus.groovy.modules.http-builder:http-builder:0.7.1')
    pluginLibs( 'org.eclipse.jgit:org.eclipse.jgit:3.7.1.201504261725-r') {
        exclude module: 'slf4j-api'
        exclude module: 'jsch'
        exclude module: 'commons-logging'
    }

    testCompile "org.codehaus.groovy:groovy-all:2.3.7"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testCompile "cglib:cglib-nodep:2.2.2"
    testCompile 'org.objenesis:objenesis:1.4'
}


task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')
        attributes 'Rundeck-Plugin-Name' : pluginName
        attributes 'Rundeck-Plugin-Description' : pluginDescription
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '2.10.1+'
        attributes 'Rundeck-Plugin-Tags': 'java,workflow step,resource model,mesos'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/mesos-steps'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': project.version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
    dependsOn(copyToLib)
}


task wrapper(type: Wrapper) {
  gradleVersion = '3.3'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

